<program-list> -> <function> <program-list> 
           | ;;

<function> -> <Type> <Name> ( <variable-list> ) <code-block> ;;

<variable-list> -> <Type> <Name> , <variable-list>
                 | <Type> <Name> 
				 | ;;

<code-block> -> { <code-list> } ;;

<code-list> -> <operator> <code-list> 
              |  ;;

<operator> -> <conditional-operator> 
            | <declaration-operator> 
			| <array-declaration-operator>
			| <while-cycle>
			| <return-operator>
			| <expression> ; ;;

<conditional-operator> -> if ( <expression> ) <code-block> else <code-block> ;;

<array-declaration-operator> -> <Type> <Name> [ <Const> ] ; ;;

<declaration-operator> -> <Type> <Name> ; ;;

<while-cycle> -> while ( <expression> ) <code-block> ;;

<return-operator> -> return <expression> ; ;;

<expression> -> <bin-expression> 
              | <un-expression>
			  | <call-expression>
			  | <const-expression>
			  | <name-expression> ;;

<bin-expression> -> ( <expression> <Operator> <expression> ) ;;
<un-expression> -> ( <Operator> <expression> ) ;;
<call-expression> -> <Name> ( <argument-list> ) ;;
<const-expression> -> <Const> ;;
<name-expression> -> <Name> ;;
			  
<argument-list> -> <expression> , <argument-list>
                 | <expression>
				 | ;;